#!/usr/bin/perl

# scotty-rel - Scotty RELOADED Network Management Tool
#
# Authors:
#   Thomas Liske <thomas@fiasko-nw.net>
#
# Copyright Holder:
#   2012 (C) Thomas Liske [http://fiasko-nw.net/~thomas/]
#
# License:
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this package; if not, write to the Free Software
#   Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301 USA
#

use Scotty::BackendConfig;
use Event;
use Getopt::Std;
use Log::Dispatch;
use Log::Dispatch::Screen;
use Log::Dispatch::Syslog;
use Proc::Daemon;
use IO::Socket;
use POSIX qw(mkfifo F_SETFL O_NONBLOCK);
use JSON;
use strict;
use warnings;

our $opt_l = '127.0.0.1';
our $opt_p = 1701;
our $opt_d;
getopts('l:p:d');

my $mypid = $$;
my @nodes = ();

my $logfmt = sub { my %p = @_; chomp($p{'message'}); return ("[$p{level}] $p{message}\n"); };
my $logffile = sub { my %p = @_;  return (scalar localtime())." [$$] $p{message}"; };
$main::logger = Log::Dispatch->new(callbacks => $logfmt);
$main::logger->add(
    Log::Dispatch::Screen->new(
	name => 'console',
	min_level => ($opt_d ? 'info' : 'notice'),
    )
);

$main::json = JSON->new->utf8;

sub WARN_handler {
    my($signal) = @_;

	if(defined $main::logger) {
		$main::logger->warning("WARNING: $signal");
	}
	else {
		printf STDERR "WARNING: $signal\n";
	}
}

sub DIE_handler {
    my($signal) = @_;

	if(defined $main::logger) {
		$main::logger->critical("ERROR: $signal");
	}
	else {
		printf STDERR "ERROR: $signal\n";
	}

	exit 1;
}

$SIG{__WARN__} = 'WARN_handler';
$SIG{__DIE__}  = 'DIE_handler';
$SIG{PIPE} = 'IGNORE';
$SIG{CHLD} = 'IGNORE';

END {
	if($mypid == $$) {
		my $m = "terminated (rc=$?)";
		if(defined $main::logger) {
			$main::logger->notice($m);
		}
		else {
			print STDERR "$m\n";
		}
	}
}

#Proc::Daemon::Init;

my $uds = IO::Socket::INET->new(
    Listen => 5,
    LocalPort => $opt_p,
    LocalAddr => $opt_l,
    Proto => 'tcp',
    ReuseAddr => 1,
) or die "$@\n";
$main::logger->notice("Listening $opt_l:$opt_p...");

$uds->sockopt(F_SETFL, O_NONBLOCK);

Event->io(
	desc => 'uds',
	fd => $uds,
	poll => 'r',
	cb => \&accept_handler,
	repeat => '1',
);

my @clients;
sub accept_handler {
    my $event = shift;
    my $h = $event->w->fd;

    my $client = $uds->accept();
    return unless defined($client);

    $main::logger->info("incoming connection");
    print $client $main::json->encode(['map', Scotty::Sensor::getMap()])."\n";
    print $client $main::json->encode(['series', Scotty::Sensor::getSeries()])."\n";

    push(@clients, $client);
}

sub sensor_data {
    my $event = shift;
    my $h = $event->w->fd;
    return if eof($h);

    my $l = <$h>;
    chomp($l);

    foreach my $client (@clients) {
	print $client "$l\n";
    }
}

&Scotty::BackendConfig::parse_config(shift);
&Scotty::Sensor::start_worker();

$main::logger->info("enter event loop");

Event::loop();
